; State machines to emit HDLC encoded data
.define FLAG_SENT_IRQ 0     ; IRQ when flag is completed
.define CLK_INTERRUPT 4     ; IRQ number for clock falling edge

;Simple program to generate a 500KHz TX clock
.program clock_tx
; Should run at 1Mhz
.side_set 1
.wrap_target
wait_clock_enable:
jmp pin enable_clock side 0     ;Loop if the clock is not enabled
jmp wait_clock_enable side 0
enable_clock:
nop side 1 [1]                  ; Push clock to 1
irq CLK_INTERRUPT side 0        ; Push clock to 0 and rise interrupt
.wrap

% c-sdk {
/**
    Initializes the clock_tx PIO program
    @param pio PIO engine to use
    @param sm State machine number
    @param offset State machine program offset
    @param clkPin Clock pin
**/
static inline void clock_tx_program_init(PIO pio, uint sm, uint offset, uint clkPin, uint clkEnPin) {
    pio_sm_config c = clock_tx_program_get_default_config(offset);

    // Only one out pin used for clock
    pio_sm_set_consecutive_pindirs(pio, sm, clkPin, 1, true);
    pio_gpio_init(pio, clkPin);
    sm_config_set_sideset_pins(&c, clkPin);

    //Jump pin is the clock enable pin
    sm_config_set_jmp_pin(&c, clkEnPin);

    //Sets the clock divider to have 2Mhz (we are using 4 instructions)
    sm_config_set_clkdiv(&c, 62.5f);

    // Load our configuration, and jump to the start of the program
    pio_sm_init(pio, sm, offset, &c);
    // Set the state machine running
    pio_sm_set_enabled(pio, sm, true);
}
%}

; Program to send data to HDLC bus
.program hdlc_tx
.wrap_target
wait_clock_enable:
jmp pin send_flag_first_zero   ;Loop if the clock is not enabled
jmp wait_clock_enable

send_flag_first_zero:
; Send the flag
irq FLAG_SENT_IRQ           ; Interrupt the MCU to signal the flag is going to output
wait 1 irq CLK_INTERRUPT    ; Waits for the clock to be low
set pins, 0                 ; Sets the pin to 0
set x, 5                    ; Prepare to send 6 consecutive ones
set y, 4                    ; Prepare to send no more than 5 ones
send_flag_ones:
wait 1 irq CLK_INTERRUPT    ; Waits for the clock to be low
set pins, 1                 ; Sets the pin to 1
jmp x-- send_flag_ones      ; Loop until we send 6 ones
send_flag_last_zero:
;Send the remaining 0
wait 1 irq CLK_INTERRUPT    ; Waits for the clock to be low
set pins, 0                 ; Sets the pin to 0
;Wait for OSR to be filled
jmp !osre tx_data           ; Jump to tx_data if OSR is not empty
; No more data
jmp wait_clock_enable       ; OSR empty send flag again

tx_data:
out x, 1                    ; Get one bit from OSR
jmp !x tx_zero              ; Jump to tx_zero if we have a 0
;Got a one
tx_one:
wait 1 irq CLK_INTERRUPT    ; Waits for the clock to be low
set pins, 1                 ; Sets the pin to 1
jmp y-- next_bit            ; Loop again if not send 5 1 in a row
; 5 consecutive 1 sent, insert a 0
tx_zero:
wait 1 irq CLK_INTERRUPT    ; Waits for the clock to be low
set pins, 0                 ; Sets the pin to 0
set y, 4                    ; Reset one counter to 4 for counting 5 ones

next_bit:
jmp !osre tx_data           ; Jump to tx_data if OSR is not empty
.wrap                       ; TX completed, send flag

% c-sdk {
/**
    Initializes the hdlc_tx PIO program
    @param pio PIO engine to use
    @param sm State machine number
    @param offset State machine program offset
    @param dataPin Data pin
**/
static inline void hdlc_tx_program_init(PIO pio, uint sm, uint offset, uint dataPin, uint clkEnPin) {
    pio_sm_config c = hdlc_tx_program_get_default_config(offset);

    // Only one out pin used for data
    pio_sm_set_consecutive_pindirs(pio, sm, dataPin, 1, true);
    sm_config_set_set_pins(&c, dataPin, 1);
    pio_gpio_init(pio, dataPin);

    //Jump pin is the clock enable pin
    sm_config_set_jmp_pin(&c, clkEnPin);

    //Configure the TX FIFO
    //Join TX and RX FIFO as we only do TX
    sm_config_set_fifo_join(&c, PIO_FIFO_JOIN_TX);
    //Shift right, with autopush threshold of 8 bits
    sm_config_set_out_shift(&c, true, true, 8);

    // Load our configuration, and jump to the start of the program
    pio_sm_init(pio, sm, offset, &c);
    // Set the state machine running
    pio_sm_set_enabled(pio, sm, true);
}
%}